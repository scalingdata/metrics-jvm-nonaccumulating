plugins {
    id 'java'
    id 'maven'
    id "com.jfrog.bintray" version "1.1"
    id 'net.researchgate.release' version '2.4.0'
}

repositories {
    mavenLocal()
    mavenCentral()
    maven {
        url "http://repository.rocana.com/content/groups/com.scalingdata"
    }
}

dependencies {
    compile 'org.apache.commons:commons-lang3:3.0'
    compile 'io.dropwizard.metrics:metrics-core:3.1.0'
    compile 'io.dropwizard.metrics:metrics-jvm:3.1.0'

    testCompile group: 'junit', name: 'junit', version: '4.11'
    testCompile(group: 'org.mockito', name: 'mockito-all', version: '1.9.5')
}

//publish a jar for Java source files
task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

//publish a jar for JavaDocs
task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

artifacts {
    archives sourcesJar
    archives javadocJar
}

task wrapper(type: Wrapper) {
    description = "Generates a new Gradle Wrapper"
    group = "build"
    gradleVersion = '2.4'
}

release {
    tagTemplate = 'metrics-jvm-nonaccumulating-${version}'

    // Only for testing, remove before merging to master
    git {
        requireBranch = ''
    }
}

uploadArchives {
    repositories {
        mavenDeployer {
            repository(url: "http://repository.rocana.com/content/repositories/com.scalingdata.releases") {
                // The username and password are set either in ~/.gradle/gradle.properties or by passing -P in the shell
                authentication(userName: rocanaReleaseUser, password: rocanaReleasePassword)
            }

            snapshotRepository(url: "http://repository.rocana.com/content/repositories/com.scalingdata.snapshots") {
                // The username and password are set either in ~/.gradle/gradle.properties or by passing -P in the shell
                authentication(userName: rocanaSnapshotUser, password: rocanaSnapshotPassword)
            }
        }
    }
}

afterReleaseBuild.dependsOn uploadArchives